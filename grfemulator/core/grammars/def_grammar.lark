// priority
// 1. min
// 2. comp
// 3. rec

start: [def ([" "] def)*]
def: name "=" rec_exp
?rec_exp: comp_exp
        | rec_exp "<-" comp_exp                    -> rec
?comp_exp: min_exp
         | min_exp "{" rec_exp (" " rec_exp)* "}"  -> comp // TODO: Вот тут могут быть проблемы!!!
?min_exp: atom
        | min_exp "?" INT                          -> min
?atom: func
     | "(" rec_exp ")"
?func: name
     | i
     | const
name: CNAME
i: "I" "^" INT "_" INT
const: INT "^" INT


%import common.CNAME
%import common.INT
%import common.WS
%import common.SH_COMMENT
%import common.C_COMMENT
%ignore SH_COMMENT         // Comment of the form # ... (to end of line)
%ignore C_COMMENT          // Comment of the form /* ... */
%ignore WS

